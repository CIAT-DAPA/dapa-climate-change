/* &r cut_process_ensemble.aml T:\gcm\cmip5\downscaled D:\jetarapues\Request\Request_pablo\hti0 D:\jetarapues\Request\Request_pablo\mask\hti0 ALL 30s prec NO YES

/* rootdir = Root folder where are storaged GCM CMIP5 data
/* outputdir = Output folder
/* mask =  Input mask data defining areas to extract
/* rcp = escenarios: rcp26 rcp45 rcp60 rcp85. Use ALL to select all the rcps 
/* resol = The possibilities are 30s 2.5min 5min 10min
/* var = The possibilities are bio, prec, tmin, tmax, tmean
/* period
/* switch = Set YES to convert outputs in ESRI-Ascii files
/* wcl = worldclim
/* tif = convert to tiff

&args rootdir outputdir mask rcp resol var period switch wcl tif
&if [null %rootdir%] &then &return Use the root_folder argument
&terminal 9999
&sys cls

&amlpath .
&s bdir [show &amlpath]

&if %resol% EQ 30s &then &s wcl_res Global_30s
&else &if %resol% EQ 2.5min &then &s wcl_res global_2_5min
&else &if %resol% EQ 5min &then &s wcl_res global_5min
&else &s wcl_res global_10min

&if %period% EQ ALL &then &s period 2020_2049 2040_2069 2060_2089 2070_2099
&else &s period %period%

&if %wcl% EQ YES &then 
	&do
		&ty          /\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/
		&ty          	/\/\/\/ CUT WORLDCLIM /\/\/\/\
		&ty          /\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/
		&if not [exists %outputdir%\wcl_%resol%\%var%_wcl_%resol%_cut_done.txt -FILE] &then
			&do	
				&if not [exists %outputdir%\wcl_%resol% -dir] &then &sys md %outputdir%\wcl_%resol%
				&r cut_GCM.aml S:\observed\gridded_products\worldclim\%wcl_res% %outputdir%\wcl_%resol% %mask% %var% %switch% %tif%
				&sys dir /d /b /o %outputdir%\wcl_%wcl_res%\* > %outputdir%\wcl_%resol%\%var%_wcl_%resol%_cut_done.txt
			&end
	&end
	
	
&if %rcp% EQ ALL &then &s rcp rcp26 rcp45 rcp60 rcp85
&else &s rcp %rcp%

&do rcp &list %rcp%
	&if %resol% EQ 30s &then &s inputdir %rootdir%\ensemble\%rcp%\Global_30s
	&else &if %resol% EQ 2.5min &then &s inputdir %rootdir%\ensemble\%rcp%\global_2_5min
	&else &if %resol% EQ 5min &then &s inputdir %rootdir%\ensemble\%rcp%\global_5min
	&else &s inputdir %rootdir%\ensemble\%rcp%\global_10min

	&if not [exists %outputdir%\%rcp%_extracts_ens -dir] &then &sys md %outputdir%\%rcp%_extracts_ens

	&if %resol% EQ 30s &then &s outdir %outputdir%\%rcp%_extracts_ens\Global_30s
	&else &if %resol% EQ 2.5min &then &s outdir %outputdir%\%rcp%_extracts_ens\Global_2_5min
	&else &if %resol% EQ 5min &then &s outdir %outputdir%\%rcp%_extracts_ens\Global_5min
	&else &s outdir %outputdir%\%rcp%_extracts_ens\Global_10min
	
	&do ts &list %period% /* 2020_2049 2040_2069 2060_2089 2070_2099		
		&s outmoddir %outdir%\%ts%
		&if not [exists %outmoddir% -dir] &then &sys md %outmoddir%

			&s outmodtsdir %outmoddir%
			&s inmodtsdir %inputdir%\%ts%
			
			&if not [exists %outdir%\%ts%_cut_done.txt -FILE] &then
				&do
					&if not [exists %outmodtsdir% -dir] &then &sys md %outmodtsdir%
					&r cut_GCM.aml %inmodtsdir% %outmodtsdir% %mask% %var% %switch% %tif%
					&sys dir /d /b /o %outmodtsdir%\* > %outdir%\%ts%_%var%_cut_done.txt
				&end
			&else 
				&do
					&ty 
					&ty Model (%ts%, %resol%) processed
				&end		
	&end 
&end	


	
&ty	
&ty ======DONE!=======

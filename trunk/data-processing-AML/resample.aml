/* NEAREST BILINEAR CUBIC SEARCH
&args indir outdir preffix res method
&terminal 9999
&sys cls
&messages &off

&if [null %indir%] OR [null %outdir%] OR [null %preffix%] OR [null %res%] OR [null %method%] &then
	&do
		&ty
		&ty INCORRECT COMMAND SYNTAX
		&ty TRY: &r resample.aml INPUT_DIR OUTPUT_DIR PREFFIX RESOLUTION METHOD
		&ty
		&ty Note: Use ALL as preffix if you wish to process all the grids within your Input dir.
		&ty Note: Resolution must be in arc-minutes
		&ty Note: Method should be one of the following: NEAREST, BILINEAR, CUBIC, SEARCH
		&ty
		&messages &on
		&return
	&end
&else
	&do
		&amlpath .
		w [show &amlpath]
		
		&if [show program] NE GRID &then GRID
		&if not [exists %outdir% -dir] &then &sys md %outdir%
		
		&if [locase %preffix%] EQ all &then &s gridlist [listfile %indir%\* -grid %outdir%\filelist.list]
		&else &s gridlist [listfile %indir%\%preffix%* -grid %outdir%\filelist.list]
		
		&if %gridlist% EQ 0 &then
			&do
				&ty
				&ty There are no gridfiles within your desired input folder. Bailing out...
				&ty 
				&if [show program] EQ GRID &then QUIT
				&call cleandirs
				&return
			&end
		&else
			&do
				&ty
				&ty Preparing to process %gridlist% files
				&ty
				
				&s resd [calc %res% / 60]
				
				&s openlist [OPEN %outdir%\filelist.list OPENSTATUS -READ]
				
				&do file = 1 &to %gridlist%
					
					&s gridname [READ %openlist% READSTATUS]
					
					&ty .-Resampling file %gridname% ([substr [calc %file% / %gridlist% * 100] 1 5]'%')
					
					&if [exists %outdir%\%gridname% -file] &then kill %outdir%\%gridname% all
					%outdir%\%gridname% = RESAMPLE(%indir%\%gridname%, %resd%, %method%)
					
				&end
				
				&s closelist [CLOSE %openlist%]
				&if [exists %outdir%\filelist.list -file] &then &sys del %outdir%\filelist.list
				
				&ty
				&ty Done!
				&ty
				
			&end
	&end
&call cleandirs
&return
/********************************************************************************
&routine cleandirs
	&if [show program] EQ GRID &then QUIT
	&if [exists log -file] &then &sys del log
	&if [exists %outdir%\filelist.list -file] &then &sys del %outdir%\filelist.list
	&messages &on
&return
/********************************************************************************
&args indir outdir timeslice gcmlist 
&terminal 9999
&sys cls
&messages &off
&if [show program] NE GRID &then GRID

&amlpath .
&s amldir [show &amlpath]

&if [null %indir%] OR [null %outdir%] OR [null %timeslice%] OR [null %gcmlist%] &then
	&do
		&ty
		&ty Wrong command syntax, run the AML appropriately
		&ty '&RUN averageGCMAnomalies.aml INDIR OUTDIR TIMESLICE GCMLIST'
		&if [show program] EQ GRID &then QUIT
		&if [exists log -file] &then &sys del log
		&return
	&end

&s varlist prec,tmean,tmin,tmax
&s nvars [token %varlist% -count]

&do v = 1 &to %nvars%
	
	&s varname [extract %v% %varlist%]
	
	&ty
	&ty Processing %varname%
		
	&s openlist [open %gcmlist% openstatus -read]
	&s gcm [read %openlist% readstatus]
	
	&s gcmcounter 1
	
	&do &while %readstatus% EQ 0
		
		&ty GCM ... %gcm% (%gcmcounter%)
		
		&s indir_gcm %indir%\%gcm%
		&s indir_gcm_ts %indir_gcm%\%timeslice%
		
		&s outdir_ts %outdir%\%timeslice%
		&if not [exists %outdir_ts% -dir] &then &sys md %outdir_ts%
		
		SETWINDOW %indir_gcm_ts%\%varname%_1
		SETMASK OFF
		SETCELL MINOF
		
		&s vlist [listfile %indir_gcm_ts%\%varname%_* -grid]
		
		arc w %indir_gcm_ts%
		
		&if %varname% EQ prec &then
			&do
				&if [exists %outdir_ts%\temp%gcmcounter% -grid] &then kill %outdir_ts%\temp%gcmcounter% all
				%outdir_ts%\temp%gcmcounter% = SUM(%vlist%)
			&end
		&else
			&do
				&if [exists %outdir_ts%\temp%gcmcounter% -grid] &then kill %outdir_ts%\temp%gcmcounter% all
				%outdir_ts%\temp%gcmcounter% = MEAN(%vlist%)
			&end
		
		arc w %amldir%
		
		&if %gcmcounter% EQ 1 &then
			&do
				&s glist temp%gcmcounter%
			&end
		&else
			&do
				&s glist %glist%,temp%gcmcounter%
			&end
		
		&s gcm [read %openlist% readstatus]
		&s gcmcounter [calc %gcmcounter% + 1]
	&end
	
	&s closelist [close %openlist%]
	
	&ty #GCMs ... [calc %gcmcounter% - 1]
	
	arc w %outdir_ts%
	
	SETWINDOW temp1
	SETMASK OFF
	SETCELL MINOF
	
	&if [exists %outdir_ts%\%varname% -grid] &then kill %outdir_ts%\%varname% all
	%outdir_ts%\%varname% = STD(%glist%)
	
	arc w %amldir%
	
	&do gc = 1 &to %gcmcounter%
		&if [exists %outdir_ts%\temp%gc% -grid] &then kill %outdir_ts%\temp%gc% all
	&end
		
&end

&if [show program] EQ GRID &then QUIT
&if [exists log -file] &then &sys del log
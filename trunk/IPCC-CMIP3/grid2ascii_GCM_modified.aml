&args inputfolder outputfolder outcopyfolder switch
&terminal 9999
/*&sys cls
&messages &off

&if [null %inputfolder%] OR [null %switch%] &then
	&do
		&sys cls
		&ty
		&ty Incorrect command syntax
		&ty
		&ty Run the command as: '&RUN INPUT_FOLDER COMPRESS'
		&ty
		&ty INPUT_FOLDER:   Folder where your grids are located
		&ty 
		&ty	OUTPUT_FOLDER:	Folder where your asciis will be create
		&ty
		&ty COMPRESS:       Write YES if you want to compress your
		&ty                 outputs into a zipfile (you will need 7zip)
		&ty                 to save hard disk space or NO whether you
		&ty                 want to leave them uncompressed
		&ty
		&ty Note:           your ascii grids will be created in
		&ty                 a folder named _asciis inside your input
		&ty                 folder.
		&ty
		&return
	&end


&if [locase %switch%] EQ yes OR [locase %switch%] EQ no &then
	&do

		&amlpath .
		&s basedir [show &amlpath]

		&s outfolder %outputfolder%\_asciis
		&if not [exists %outfolder% -dir] &then &sys md %outfolder%

		
		&ty          /\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/
		&ty          /\/\/\/\GCM MODELS ASCII2GRID/\/\/\/\
		&ty          /\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/
		
		&s gridlist [listfile %inputfolder%\* -grid %outfolder%\listgrids.list]
		&s openlist [OPEN %outfolder%\listgrids.list OPENSTATUS -READ]
		
		&if %OPENSTATUS% NE 0 &then
			&do
				&ty
				&ty The file %basedir%\listgrids.list does not exist or cannot be opened
				&return
			&end
		&else
			&do
				&do i = 1 &to %gridlist%
					&s gridname [READ %openlist% READSTATUS]
					
					&s vartype [before %gridname% _]
					
					&ty
					&ty   -> .Processing %gridname%
					
					&if [exists %outfolder%\%gridname%.prj -file] &then &s delstat [DELETE %outfolder%\%gridname%.prj -file]
					&if [exists %outfolder%\%gridname%.asc -file] &then &s delstat [DELETE %outfolder%\%gridname%.asc -file] /* &ty Output grid exists
					GRIDASCII %inputfolder%\%gridname% %outfolder%\%gridname%.asc
					
					&if [locase %switch%] EQ yes &then
						&do
							&sys 7za a %outfolder%\%vartype%_asc.zip %outfolder%\%gridname%.asc
							&if [exists %outfolder%\%gridname%.asc -file] &then &s delstat [DELETE %outfolder%\%gridname%.asc -file]
							&if [exists %outfolder%\%gridname%.prj -file] &then &s delstat [DELETE %outfolder%\%gridname%.prj -file]
						&end
					&else &ty No compressing selected
					
				&end
			&end
		&s closefile [close %openlist%]
		&if [show program] EQ GRID &then QUIT
		
	&end
&else
	&do
		&sys cls
		&ty
		&ty You need to type YES or NO in the COMPRESS argument
		&ty Bailing out...
		&ty 
		&return
	&end

&if [exists %outfolder%\listgrids.list -file] &then &sys del %outfolder%\listgrids.list
&if [exists %basedir%\log -file] &then &sys del %basedir%\log

&ty
&ty   -> .Now copying...

&s finalfolder %outcopyfolder%\_asciis
&if not [exists %finalfolder% -dir] &then &sys md %finalfolder%

&s copystat [COPY %outfolder% %finalfolder% -DIRECTORY]
&if %copystat% EQ 0 &then 
	&do
		&ty   -> .Successfully done!
		&s delstat [DELETE %outputfolder% -DIRECTORY]
	&end
&else
	&do
		&ty   An error occurred during calculations, bailing out...
		&return
	&end

&ty
&ty Process done!
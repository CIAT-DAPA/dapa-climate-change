&args inputfolder outputfolder modname rcpmod tsmod resol switch
&terminal 9999
/*&sys cls
&messages &off

&if [null %inputfolder%] OR [null %outputfolder%] OR [null %modname%] OR [null %rcpmod%] OR [null %tsmod%] OR [null %resol%] OR [null %switch%] &then &return Use inputfolder outputfolder modname rcpmod tsmod switch

&amlpath .
&s basedir [show &amlpath]

&ty          /\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/
&ty          /\/\/\/\GCM MODELS ASCII2GRID/\/\/\/\
&ty          /\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/
  
&s gridlist [listfile %inputfolder%\* -grid %outputfolder%\listgrids.list]
&s openlist [OPEN %outputfolder%\listgrids.list OPENSTATUS -READ]
  
&if %OPENSTATUS% NE 0 &then
	&do
		&ty
		&ty The file %outputfolder%\listgrids.list does not exist or cannot be opened
		&return
	&end
&else
	&do
		&do i = 1 &to %gridlist%

			&s gridname [READ %openlist% READSTATUS]
			&s vartype [before %gridname% _]
			
			&ty
			&ty   -> .Processing %gridname%
			
			&if not [exists %outputfolder%\%vartype% -dir] &then &sys md %outputfolder%\%vartype%
			&if [exists %outputfolder%\%vartype%\%gridname% -grid] &then &ty Grid %gridname% exists
			&else COPY %inputfolder%\%gridname% %outputfolder%\%vartype%\%gridname%
			
		&end
		
		&do vartype &list bio cons prec tmax tmean tmin
			
			&ty   -> .Compressing %vartype%
			&sys 7za a -tzip -mmt=4 %outputfolder%\%modname%_%rcpmod%_%tsmod%_%vartype%_%resol%_r1i1p1_no_tile_grd.zip %outputfolder%\%vartype%
			&s delstat [DELETE %outputfolder%\%vartype% -DIR]
			
		&end
		
	&end
	
&s closefile [close %openlist%]
&if [show program] EQ GRID &then QUIT

&if [exists %outputfolder%\listgrids.list -file] &then &sys del %outputfolder%\listgrids.list
&if [exists %basedir%\log -file] &then &sys del %basedir%\log

&ty
&ty Process done!
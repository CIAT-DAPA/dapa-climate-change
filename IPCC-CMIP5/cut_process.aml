/* &r cut_process.aml T:\gcm\cmip5\downscaled D:\jetarapues\Request\Request_ocampo D:\jetarapues\Request\Request_ocampo\mask\cod0 rcp26 30s ALL YES YES
/* &r cut_process.aml T:\gcm\cmip5\downscaled D:\jetarapues\Request\Request_ocampo\pan S:\admin_boundaries\grid_files\pan_adm\pan0 rcp26 30s prec 2020_2049 bcc_csm1_1 YES YES NO

/* rootdir = Root folder where are storaged GCM CMIP5 data
/* outputdir = Output folder
/* mask =  Input mask data defining areas to extract
/* rcp = escenarios: rcp26 rcp45 rcp60 rcp85. Use ALL to select all the rcps 
/* resol = The possibilities are 30s 2.5min 5min 10min
/* var = The possibilities are bio, prec, tmin, tmax, tmean
/* switch = Set YES to convert outputs in ESRI-Ascii files
/* wcl = worldclim
/* period = ALL: 2020_2049 2040_2069 2060_2089 2070_2099
/* models = ALL or giss_e2_h
/* tif = convert to tiff

&args rootdir outputdir mask rcp resol var period models switch wcl tif
&if [null %rootdir%] &then &return Use the root_folder argument
&terminal 9999
&sys cls

&amlpath .
&s bdir [show &amlpath]


&if %resol% EQ 30s &then &s wcl_res global_30s
&else &if %resol% EQ 2.5min &then &s wcl_res global_2_5min
&else &if %resol% EQ 5min &then &s wcl_res global_5min
&else &s wcl_res global_10min

&if %rcp% EQ ALL &then &s rcp rcp26 rcp45 rcp60 rcp85
&else &s rcp %rcp%

&if %period% EQ ALL &then &s period 2020_2049 2040_2069 2060_2089 2070_2099
&else &s period %period%

&if %models% EQ ALL &then &s models [listfile %rootdir%\%rcp%\%wcl_res%\* -dir]
&else &s models %models%

&do rcp &list %rcp%
	&if %resol% EQ 30s &then &s inputdir %rootdir%\%rcp%\global_30s
	&else &if %resol% EQ 2.5min &then &s inputdir %rootdir%\%rcp%\global_2_5min
	&else &if %resol% EQ 5min &then &s inputdir %rootdir%\%rcp%\global_5min
	&else &s inputdir %rootdir%\%rcp%\global_10min

	&if not [exists %outputdir%\%rcp%_extracts -dir] &then &sys md %outputdir%\%rcp%_extracts

	&if %resol% EQ 30s &then &s outdir %outputdir%\%rcp%_extracts\global_30s
	&else &if %resol% EQ 2.5min &then &s outdir %outputdir%\%rcp%_extracts\Global_2_5min
	&else &if %resol% EQ 5min &then &s outdir %outputdir%\%rcp%_extracts\Global_5min
	&else &s outdir %outputdir%\%rcp%_extracts\Global_10min

	/* &s modellist [listfile %rootdir%\%rcp%\global_30s\* -dir]
	/*&s modellist giss_e2_h mohc_hadgem2_es ipsl_cm5a_lr ncc_noresm1_m 
	&s modellist %models%
	&s nmodels [token %modellist% -count]
	&do md = 1 &to %nmodels%
		
		&s modname [extract %md% %modellist%]
		&s outmoddir %outdir%\%modname%\r1i1p1
		&if not [exists %outmoddir% -dir] &then &sys md %outmoddir%
		
		&do ts &list %period% /* 2020_2049 2040_2069 2060_2089 2070_2099
			&s outmodtsdir %outmoddir%\%ts% 
			&s inmodtsdir %inputdir%\%modname%\r1i1p1\%ts%
			
			&if not [exists %outdir%\%modname%\r1i1p1\%ts%_cut_done.txt -FILE] &then
				&do
					&ty
					&ty Model %modname% (%rcp%, %ts%, %resol%)
					&if not [exists %outmodtsdir% -dir] &then &sys md %outmodtsdir%
					&r cut_GCM.aml %inmodtsdir% %outmodtsdir% %mask% %var% %switch% %tif%
					/* &sys dir /d /b /o %outmodtsdir%\* > %outdir%\%modname%\r1i1p1\%ts%_cut_done.txt
				&end
			&else 
				&do
					&ty 
					&ty Model %modname% (%ts%, %resol%) processed
				&end		
			
		&end
	&end	
&end

&if %wcl% EQ YES &then 
	&do
		&ty          /\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/
		&ty          	/\/\/\/ CUT WORLDCLIM /\/\/\/\
		&ty          /\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/
		&if not [exists %outputdir%\wcl_%resol%\_asciis\%var%_asc.zip -FILE] &then
			&do	
				&if not [exists %outputdir%\wcl_%resol% -dir] &then &sys md %outputdir%\wcl_%resol%
				&r cut_GCM.aml S:\observed\gridded_products\worldclim\%wcl_res% %outputdir%\wcl_%resol% %mask% %var% %switch% %tif%
				/* &sys dir /d /b /o %outputdir%\wcl_%wcl_res%\* > %outputdir%\wcl_%wcl_res%\wcl_%wcl_res%_cut_done.txt
			&end
	&end
	
&ty	
&ty ======DONE!=======

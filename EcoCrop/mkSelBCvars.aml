/* MkBCvars.AML 
/* /*
/* Author Robert Hijmans
/* January 2006  
/* rhijmans@uclink.berkeley.edu
/*
/* Version 2.3
/*
/* This AML creates the 19 BIOCLIM variables from 
/* monthly Tmin, Tmax, and Precipitation grids
/* The results are rounded where integers would become reals
/* (I assume that input values were multiplied by 10
/* and stored as Integers to begin with)
/* P2 is first multiplied by 10
/* CVs are first multiplied by 100.
/*
/* rounding of "x" is done with "int(floor(x + 0.5))"
/* because "int(x+0.5)" as suggested by ESRI (see INT in Arc Help), does not
/* round negative numbers correctly (-2.6 -> -2 intstead of -3)
/*
/* You must change the first four lines (input files and output directory)
/* If you do not have average temperature, create it with the lines that follow
/*
/* Also note that the AML removes some temporary grids if they exist 
/* (the first "&do i = 0 &to 15" bit)
/* Please make sure that you do not have files
/* with those names that you want to keep.
/*
/* BIO1 = Annual Mean Temperature
/* BIO2 = Mean Diurnal Range (Mean of monthly (max temp - min temp))
/* BIO3 = Isothermality (P2/P7) (* 100)
/* BIO4 = Temperature Seasonality (standard deviation *100)
/* BIO5 = Max Temperature of Warmest Month
/* BIO6 = Min Temperature of Coldest Month
/* BIO7 = Temperature Annual Range (P5-P6)
/* BIO8 = Mean Temperature of Wettest Quarter 
/* BIO9 = Mean Temperature of Driest Quarter
/* BIO10 = Mean Temperature of Warmest Quarter
/* BIO11 = Mean Temperature of Coldest Quarter
/* BIO12 = Annual Precipitation
/* BIO13 = Precipitation of Wettest Month
/* BIO14 = Precipitation of Driest Month
/* BIO15 = Precipitation Seasonality (Coefficient of Variation)
/* BIO16 = Precipitation of Wettest Quarter
/* BIO17 = Precipitation of Driest Quarter
/* BIO18 = Precipitation of Warmest Quarter
/* BIO19 = Precipitation of Coldest Quarter
/* 
/* These summary Bioclimatic variables are after:
/*   Nix, 1986. A biogeographic analysis of Australian elapid snakes. In: R. Longmore (ed.).
/*      Atlas of elapid snakes of Australia. Australian Flora and Fauna Series 7.
/*      Australian Government Publishing Service, Canberra.
/*
/* and Expanded following the ANUCLIM manual
/*  
/* 
/* Temperature data is in units of  °C * 10 because that allows me to store the data as Integer values,
/* (with 0.1 °C precision) which is more efficient than storing the data as Real values. 
/* However, you will want to report the data in °C. Precipitation data is in mm.
/* 
/* 
&args inmonthly outselected gslength

&if [null %inmonthly%] OR [null %outselected%] OR [NULL %gslength%] &then
	&do
		&ty
		&ty INCORRECT SYNTAX
		&ty Run the aml as: '&r mkBCvars.aml INPUT_FOLDER OUTPUT_FOLDER GS_LENGTH'
		&ty
		&return
	&end
&else w %inmonthly%

&TERMINAL 9999

&s program [locase [show program]]
&if %program% ^= grid &then grid

&sv tn = tmin_
&sv tx = tmax_
&sv ta = tmean_
&sv pt = prec_

/* if TAVG does not exist.....
&do j = 1 &to 12
  &if [EXISTS %ta%%j% -grid] &then &type %ta%%j%
  &else %ta%%j% = (%tn%%j% + %tx%%j%) / 2
&end

&if [exists drym -grid] &then kill drym
&if [exists wetm -grid] &then kill wetm

&sv TAvar = %ta%1
&sv TXvar = %tx%1
&sv TNvar = %tn%1
&sv PTvar = %pt%1

&do j = 2 &to 12
  &sv tavar = %tavar%,%ta%%j% 
  &sv txvar = %txvar%,%tx%%j% 
  &sv tnvar = %tnvar%,%tn%%j% 
  &sv ptvar = %ptvar%,%pt%%j% 
&end

/* Wettest and driest (this is the only part to be modified)

&do i = 1 &to 12
  &sv gsstart = %i%
  &sv gsend = %i% + %gslength% - 1
  
  &ty Month %i% (GS %gsstart% to %gsend%)
  
  &do k = %gsstart% &to %gsend%
  	&sv j = %k%
  	&if %j% > 12 &then &sv j [calc %j% - 12]
  	
  	&if %k% EQ %gsstart% &then
  		&do
  			&sv listpgrids %pt%%j%
  			&sv listtgrids %ta%%j%
  		&end
  	&else
  		&do
  			&sv listpgrids %listpgrids%,%pt%%j%
  			&sv listtgrids %listtgrids%,%ta%%j%
  		&end
  	
  &end
  
  q%i% = SUM(%listpgrids%)
  t%i% = SUM(%listtgrids%)
&end

mnt0 = con(isnull(q1),0,100)
mnt1 = setnull(mnt0 < 1, 1)
wet1 = q1

&do i = 1 &to 11
  &sv j = [calc %i% + 1]
  
  mnt%j% = con(q%j% > wet%i%, [calc %j%], mnt%i%)
  wet%j% = con(q%j% > wet%i%, q%j%, wet%i%)
&end
wetm = mnt12

/* P16. Precipitation of Wettest X-month period 
&if [exists %outselected%\wettest -grid] &then &type Wettest exists
&else
&do
  %outselected%\wettest = wet12
  &type Wettest done
&end

&do i = 1 &to 12
  kill mnt%i%
  kill wet%i%
&end

mnt1 = setnull(mnt0 < 1, 1)
dry1 = q1

&do i = 1 &to 11
  &sv j = [calc %i% + 1]
  mnt%j% = con(q%j% < dry%i%, [calc %j%], mnt%i%)
  dry%j% = con(q%j% < dry%i%, q%j%, dry%i%)
&end
drym = mnt12

/* P17. Precipitation of Driest X-month period  
&if [exists %outselected%\driest -grid] &then &type Driest exists
&else
&do
  %outselected%\driest = dry12 
  &type Driest done
&end

&do i = 1 &to 12
  kill mnt%i%
  kill dry%i%
&end
kill mnt0

/* Warmest and coldest

mnt0 = con(isnull(t1),0,100)
mnt1 = setnull(mnt0 < 1, 1)
hot1 = t1
&do i = 1 &to 11
  &sv j = [calc %i% + 1]
  mnt%j% = con(t%j% > hot%i%, [calc %j%], mnt%i%)
  hot%j% = con(t%j% > hot%i%, t%j%, hot%i%)
&end
hotm = mnt12

/* P10 Mean Temperature of Warmest X-month period 
&if [exists %outselected%\warmest -grid] &then &type Warmest exists
&else
&do
  %outselected%\warmest = int(floor(hot12 / %gslength% + 0.5))
  &type Warmest done
&end

&do i = 1 &to 12
  kill mnt%i%
  kill hot%i%
&end


mnt1 = setnull(mnt0 < 1, 1)
cld1 = t1

&do i = 1 &to 11
  &sv j = [calc %i% + 1]
  mnt%j% = con(t%j% < cld%i%, [calc %j%], mnt%i%)
  cld%j% = con(t%j% < cld%i%, t%j%, cld%i%)
&end
cldm = mnt12

/* P11 Mean Temperature of Coldest X-month period
&if [exists %outselected%\coldest -grid] &then &type Coldest exists
&else
&do
  %outselected%\coldest = int(floor(cld12 / %gslength% + 0.5))
  &type Warmest done
&end

&do i = 1 &to 12
  kill mnt%i%
  kill cld%i%
&end
kill mnt0

kill hotm
kill cldm
kill drym
kill wetm

&do i = 1 &to 12
  kill q%i%
  kill t%i%
&end

&type Done!
&return